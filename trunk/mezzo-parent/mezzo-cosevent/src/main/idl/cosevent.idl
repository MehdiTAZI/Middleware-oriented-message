module cosevent {
	
	struct Event{
		long long timestamp;
		string content;
		long priority;
		long long datePreomption;
		
	};
	
	exception AlreadyRegisteredException{
		string message;
	};
	
	exception NotRegisteredException{
		string message;
	};
	
	exception AlreadyConnectedException{
		string message;
	};
	
	exception ChannelAlreadyExistsException{
		string message;
	};
	
	exception NotConnectedException{
		string message;
	};
	
	exception MaximalConnectionReachedException{
		string message;
	};
	
	exception ChannelNotFoundException{
		string message;
	};
	
	exception ConsumerNotFoundException{
		string message;
	};
	
	exception CannotReduceCapacityException{
		string message;
	};
	
	interface Consumer{
	};
	
	interface Supplier{
	};
	
	interface CallbackConsumer{
		void receive(in Event evt) raises (ConsumerNotFoundException);
	};
		
	interface ProxyForPushConsumer : Consumer{
			
		void subscribe(in CallbackConsumer cc) raises (ChannelNotFoundException,AlreadyRegisteredException);
		void unsubscribe() raises (ChannelNotFoundException,NotRegisteredException);
		void connect() raises (ChannelNotFoundException,AlreadyConnectedException,MaximalConnectionReachedException,NotRegisteredException);
		void disconnect() raises (ChannelNotFoundException,NotConnectedException,NotRegisteredException);
		
	};
	
	interface ProxyForPushSupplier : Supplier{
		void connect() raises (ChannelNotFoundException,AlreadyConnectedException,MaximalConnectionReachedException);
		void push(in Event evt) raises (ChannelNotFoundException, NotConnectedException);
		void disconnect() raises (ChannelNotFoundException,NotConnectedException);
	};
	
	interface ChannelAdmin{
		ProxyForPushConsumer getProxyForPushConsumer() raises (ChannelNotFoundException);
		ProxyForPushSupplier getProxyForPushSupplier() raises (ChannelNotFoundException);
	};
	
	interface EventServerChannelAdmin{
		long long createChannel(in string topic, in long capacity) raises (ChannelAlreadyExistsException);
		ChannelAdmin getChannel(in long long uniqueServerChannelId) raises (ChannelNotFoundException);
		void destroyChannel(in long long uniqueServerChannelId) raises (ChannelNotFoundException);
		void changeChannelCapacity(in long long uniqueServerChannelId, in long capacity) raises (ChannelNotFoundException,CannotReduceCapacityException);
	};
};
